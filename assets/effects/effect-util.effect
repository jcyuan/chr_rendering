// Utility effect for pipeline
// Pass 0: tone mapping
// Pass 1: copy

CCEffect %{
  techniques:
  - passes:
    - vert: vs
      frag: fs-tonemap
      pass: pipeline-util
      rasterizerState:
        cullMode: none
      depthStencilState:
        depthTest: false
        depthWrite: false
    - vert: vs
      frag: fs-copy
      pass: pipeline-util
      rasterizerState:
        cullMode: none
      depthStencilState:
        depthTest: false
        depthWrite: false
}%

CCProgram vs %{
  precision highp float;
  #include <legacy/decode-standard>
  #include <post-process/pipeline>

  out vec2 v_uv;

  void main () {
    StandardVertInput In;
    CCDecode(In);
    FLIP_VULKAN_NDC(In.position);
    gl_Position = In.position;

    v_uv = a_texCoord;
  }

}%

CCProgram fs-tonemap %{
  precision highp float;
  #include <common/color/gamma>
  #include <common/color/tone-mapping>
  
  in vec2 v_uv;

  #pragma rate inputTexture pass
  uniform sampler2D inputTexture;

  layout(location = 0) out vec4 fragColor;

  void main () {
    fragColor = texture(inputTexture, v_uv);
    #if CC_USE_FLOAT_OUTPUT
      fragColor.rgb = HDRToLDR(fragColor.rgb);
      fragColor.rgb = LinearToSRGB(fragColor.rgb);
    #endif
  }
}%

CCProgram fs-copy %{
  precision highp float;

  in vec2 v_uv;

  #pragma rate inputTexture pass
  uniform sampler2D inputTexture;

  layout(location = 0) out vec4 fragColor;

  void main () {
    fragColor = texture(inputTexture, v_uv);
  }
}%
